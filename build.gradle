plugins {
    id 'java'
    id 'jacoco'
    id 'findbugs'
    id 'maven'
    id 'signing'
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

group = 'com.after_sunrise.cryptocurrency'
archivesBaseName = 'bitflyer4j'
version = '0.3.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    testCompile {
        extendsFrom compileOnly
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.16"
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.google.inject:guice:4.1.0'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.pubnub:pubnub-gson:4.7.0'
    compile 'org.apache.commons:commons-configuration2:2.1.1'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'org.apache.commons:commons-lang3:3.6'
    runtime 'commons-beanutils:commons-beanutils:1.9.3'
    testCompile 'org.testng:testng:6.10'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.jboss.resteasy:resteasy-undertow:3.1.4.Final'
    testRuntime 'ch.qos.logback:logback-classic:1.2.3'
}

javadoc {
    options.locale = 'en_US'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar.doFirst {
    project.ext {
        vval = archivesBaseName + '.version=' + project.version
        vout = 'build/resources/main/' + archivesBaseName + '-version.properties'
    }
    ant.echo message: project.ext.vval, file: project.ext.vout
}

signing {
    required {
        !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives")
    }
    sign configurations.archives
}

uploadArchives {
    project.ext {
        url = 'https://github.com/after-the-sunrise/' + archivesBaseName
        scm = 'scm:git:https://github.com/after-the-sunrise/' + archivesBaseName + '.git'
        user = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv("ossrhUsername")
        pass = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv("ossrhPassword")
    }
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: project.ext.user, password: project.ext.pass)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: project.ext.user, password: project.ext.pass)
            }
            pom.project {
                packaging 'jar'
                name archivesBaseName
                description "Java wrapper library for the bitFlyer Lightning API."
                url project.ext.url
                licenses {
                    license {
                        name 'GNU Affero General Public License'
                        url 'https://www.gnu.org/licenses/agpl.txt'
                    }
                }
                developers {
                    developer {
                        name 'Takanori Takase'
                        email 'scm@after-sunrise.com'
                    }
                }
                scm {
                    connection project.ext.scm
                    developerConnection project.ext.scm
                    url project.ext.url
                }
            }
        }
    }
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

findbugs {
    ignoreFailures = true
}
